plugins {
    id 'java'
    id 'jacoco'
}

group = 'it.units.sdm.in20'
version = '1.0.0'

repositories {
    mavenCentral()
}

// --- BUILD ---
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// --- RUN ---
tasks.withType(JavaExec).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    testImplementation(platform("org.junit:junit-bom:5.9.1"))
    testImplementation("org.junit.jupiter:junit-jupiter")
}

test {
    useJUnitPlatform {
        if (project.hasProperty("excludeTaggedTests")) {
            excludeTags 'noCircleCI'
        }
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        xml.outputLocation = file(".coverage/jacocoReport.xml")
        html.required = false
    }

    classDirectories.setFrom(files(
        fileTree("${buildDir}/classes/java/main"),
        fileTree("${buildDir}/classes/java/test") {
            include "testDoubles/**"
        }
    ))

    sourceDirectories.setFrom(files(
        "src/main/java",
        "src/test/java/testDoubles"
    ))

    executionData.setFrom(fileTree(buildDir) {
        include "jacoco/test.exec"
    })
}

// ----

tasks.register('jar_local', Jar) {
    archiveClassifier = 'local'
    from sourceSets.main.output
    manifest {
        attributes 'Main-Class': 'it.units.sdm.quoridor.Starter'
    }
}

tasks.register('jar_server', Jar) {
    archiveClassifier = 'server'
    from sourceSets.main.output
    manifest {
        attributes 'Main-Class': 'it.units.sdm.quoridor.ServerStarter'
    }
}

tasks.register('jar_client', Jar) {
    archiveClassifier = 'client'
    from sourceSets.main.output
    manifest {
        attributes 'Main-Class': 'it.units.sdm.quoridor.ClientStarter'
    }
}


// ----

def createRunTask(String name, String mainClassName, String desc) {
    tasks.register(name, JavaExec) {
        group = "application"
        description = desc
        mainClass.set("it.units.sdm.quoridor." + mainClassName)
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
        logging.captureStandardOutput(LogLevel.QUIET)
        systemProperty 'org.gradle.console', 'plain'
    }
}

createRunTask('runLocal', 'Starter', 'Runs a local game')
createRunTask('runServer', 'ServerStarter', 'Runs the quoridor server')
createRunTask('runClient', 'ClientStarter', 'Runs a quoridor client')